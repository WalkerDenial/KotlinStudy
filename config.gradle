// Top-level build file where you can add configuration options common to all sub-projects/modules.
allprojects {
    repositories {
        maven { url local_host }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext.ver = [
        compileSdkVersion: 30,
        buildToolsVersion: "30.0.2",

        minSdkVersion    : 21,
        targetSdkVersion : 30,
        versionCode      : 1,
        versionName      : "1.0.0"
]

ext.deps = [
        kt      : [
                core: "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        ],
        andX    : [
                core      : 'androidx.core:core-ktx:1.2.0',
                appcompat : 'androidx.appcompat:appcompat:1.1.0',
                constraint: 'androidx.constraintlayout:constraintlayout:1.1.3',
                livedata  : 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0',
                viewmodel : 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
        ],
        rx      : [
                android: 'io.reactivex.rxjava3:rxandroid:3.0.0',
                java   : 'io.reactivex.rxjava3:rxjava:3.0.0'
        ],
        google  : [
                material: 'com.google.android.material:material:1.1.0'
        ],
        squareup: [
                retrofit : 'com.squareup.retrofit2:retrofit:2.9.0',
                converter: 'com.squareup.retrofit2:converter-gson:2.9.0',
                okhttp   : 'com.squareup.okhttp3:okhttp:4.9.0',
                okio     : 'com.squareup.okio:okio:2.9.0',
                adapter  : 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
        ],
        test    : [
                junit   : 'junit:junit:4.12',
                ext     : 'androidx.test.ext:junit:1.1.1',
                espresso: 'androidx.test.espresso:espresso-core:3.2.0',
                runner  : 'androidx.test.runner.AndroidJUnitRunner'
        ]
]

ext.ktDeps = { handler ->
    handler.implementation deps.kt.core
}

ext.andXDeps = { handler ->
    handler.implementation deps.andX.core
    handler.implementation deps.andX.appcompat
    handler.implementation deps.andX.constraint
    handler.implementation deps.andX.livedata
    handler.implementation deps.andX.viewmodel
}

ext.googleDeps = { handler ->
    handler.implementation deps.google.material
}

ext.testDeps = { handler ->
    handler.testImplementation deps.test.junit
    handler.androidTestImplementation deps.test.ext
    handler.androidTestImplementation deps.test.espresso
}

ext.rxDeps = { handler ->
    handler.implementation deps.rx.android
    handler.implementation deps.rx.java
}

ext.retrofitDeps = { handler ->
    handler.implementation deps.squareup.retrofit
    handler.implementation deps.squareup.converter
    handler.implementation deps.squareup.okhttp
    handler.implementation deps.squareup.okio
    handler.implementation deps.squareup.adapter
}

ext.libGroup = { handler ->
    ktDeps(handler)
    andXDeps(handler)
    testDeps(handler)
}

ext.moduleGroup = { handler ->
    libGroup(handler)
    googleDeps(handler)
    handler.implementation project(":lib_base")
}